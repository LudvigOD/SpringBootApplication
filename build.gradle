
evaluationDependsOn(':register')
evaluationDependsOn(':result')
evaluationDependsOn(':backend')

// Settings that apply to all subprojects
subprojects {

    // Apply the java plugin to add support for Java
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

}

/**
 * I think I finally managed to get the build to work. We might need
 * to customize it a bit more, but for now it works. It pulls the
 * built jar files from the subprojects and puts them in a zip file,
 * and also includes our documentation (which is still empty, btw!).
 *
 * Importantly, each subproject has its own build.gradle file, which
 * uses two boot plugins to build the jar files (this is where the
 * task bootJar comes from). The bootJar task handles all the fancy
 * magic, of which I understand very little, but it works!
 */
task assembleAll(type: Zip) {
    // Set the destination directory for the zip file
    destinationDirectory = layout.buildDirectory.dir('zip')

    // Set the base name and extension of the archive
    archiveBaseName.set('project-bundle')
    archiveExtension.set('zip')

    // Add the jar files from the subprojects
    from([
        project(':backend').bootJar,
        project(':register').bootJar,
        project(':result').bootJar,
    ])

     // Include README.md at the root of the zip
    from('docs') {
        include 'README.md'
        into '/'
    }

    // Include all other files in 'docs' into 'documentation', excluding README.md
    from('docs') {
        exclude 'README.md'
        into 'documentation'
    }
}

